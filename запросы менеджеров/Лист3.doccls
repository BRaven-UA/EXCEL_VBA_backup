Private Sub Worksheet_Activate()    ' Собирает заявки со всех листов и сортирует по дате

    total_rows = first_row

    Application.ScreenUpdating = False
    ' Очищаем лист
    range(Cells(first_row, 1), Cells(Cells.Find(what:="*", LookIn:=xlValues, SearchDirection:=xlPrevious, SearchOrder:=xlByRows).Row + 1, last_column)).ClearContents
    Call run_Fill_ranges

    ' копируем запросы из других листов
    For i = 2 To ThisWorkbook.Worksheets.Count
        With Worksheets(i)
            last_row = .range("A1:" & .Cells(1, last_column).Address).EntireColumn.Find(what:="*", LookIn:=xlValues, SearchDirection:=xlPrevious, SearchOrder:=xlByRows).Row
            If last_row >= first_row Then
                Dim r As range
                Set r = .range(.Cells(first_row, 1), .Cells(last_row, last_column))
                r.Copy Destination:=range("A" & total_rows)
                total_rows = total_rows + r.Rows.Count
            End If
        End With
    Next i

    ' Сортируем по дате
    With ThisWorkbook.Worksheets(ActiveSheet.Name).Sort
        .SortFields.Clear
        .SortFields.Add Key:=range("D" & first_row), SortOn:=xlSortOnValues, Order:=xlAscending, DataOption:=xlSortNormal
        .SetRange range(Cells(first_row, 1), Cells(total_rows, last_column))
        .Header = xlNo
        .MatchCase = False
        .Orientation = xlTopToBottom
        .SortMethod = xlPinYin
        .Apply
    End With

    ' Подгоняем высоту строк под содержимое
    ThisWorkbook.Worksheets(ActiveSheet.Name).Rows.AutoFit

    ' центрируем экран на последнем запросе
    Dim oncell As range
    Set oncell = range("D" & total_rows - 1)
    With ActiveWindow.VisibleRange
        VisRows = .Rows.Count
        VisCols = .Columns.Count
    End With
    With Application
        .Goto reference:=oncell.Parent.Cells( _
            .WorksheetFunction.Max(1, oncell.Row + _
            (oncell.Rows.Count / 2) - (VisRows / 2)), _
            .WorksheetFunction.Max(1, oncell.Column + _
            (oncell.Columns.Count / 2) - _
            .WorksheetFunction.RoundDown((VisCols / 2), 0))), _
         scroll:=True
    End With
    oncell.Select

    Application.ScreenUpdating = True

End Sub
